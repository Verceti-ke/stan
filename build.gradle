// Apply third-party plugins.
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.3"
}

// Apply core plugins.
apply plugin: 'application'

mainClassName = 'com.github.sschuberth.stan.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    funTest {
        kotlin.srcDir 'src/funTest/kotlin'
        resources.srcDir 'src/funTest/resources'
    }
}

dependencies {
    compile 'com.itextpdf:itextpdf:5.5.11'

    // See https://github.com/gradle/gradle/blob/master/subprojects/docs/src/samples/java/withIntegrationTests/build.gradle.
    funTestCompile sourceSets.main.output
    funTestCompile sourceSets.test.output
    funTestCompile configurations.testCompile
    funTestRuntime configurations.testRuntime

    funTestCompile 'io.kotlintest:kotlintest:2.0.3'
    funTestCompile 'com.github.salomonbrys.kotson:kotson:2.5.0'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task funTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'Verification'
    classpath = sourceSets.funTest.runtimeClasspath
    testClassesDirs = sourceSets.funTest.output.classesDirs
}

check.dependsOn funTest
